#---------------------------------------------------------------------------*
#                                                                           *
#   S Y N T A X                                                             *
#                                                                           *
#---------------------------------------------------------------------------*

syntax oa_parser (oa_scanner) {

rule <axiome>

rule <axiome> {
  var @M_processor outProcessorMap = @M_processor ยง[]
  var @M_network outNetworkMap = @M_network ยง[]
  var @M_messages outMessagesMap = @M_messages ยง[]
  var @M_tasks outTaskMap = @M_tasks ยง[]
  $system$
  repeat
  while
    $processor$
    $identifier$ ?let processorName
    let @luint step
    select
     step = @luint.init { !1 !@location.here}
    or
      $scalingfactor$
      $literal_integer$ ? step
      if step.uint == 0 then
        error step : "the step value must be > 0"
      end
    end
    [!?outProcessorMap insertKey !processorName ![outProcessorMap count] !step]
    $;$
  while
    let @bool network
    select
      $can$
      network = true
    or
      $van$
      network = false
    end
    $network$
    $identifier$ ?let networkName
    let @luint factor
    select
     factor = @luint.init { !1 !@location.here}
    or
      $scalingfactor$
      $literal_integer$ ? factor
      if factor.uint == 0 then
        error factor : "the factor value must be > 0"
      end
    end
    [!?outNetworkMap insertKey !networkName ![outNetworkMap count] !network !factor]
    $;$
  while
    $task$
    $identifier$ ?let taskName
    $priority$
    $literal_integer$ ?let taskPriority
    $duration$
    $literal_integer$ ?let taskDurationMin
    $..$
    $literal_integer$ ?let taskDurationMax
    let @luint taskOffset
    let @bool explicitOffset
    select
      $offset$
      $literal_integer$ ? taskOffset
      explicitOffset = true
    or
      taskOffset = @luint.init { !0 ! @location.here}
      explicitOffset = false
    end
    let @luint taskDeadline
    select
      $deadline$
      $literal_integer$ ? taskDeadline
    or
      taskDeadline = @luint.init { !4294967295 !@location.here}
    end
    let @AC_task taskKind
    let @uint taskProcessorIndex
    let @luint taskPeriod
    select
      $period$
      $literal_integer$ ? taskPeriod
      $processor$
      $identifier$ ?let taskProcessorName
      [outProcessorMap searchKey !taskProcessorName ?taskProcessorIndex ?*]
      taskKind = @C_independantTask.init
    or
      $on$
      if explicitOffset then
        error @location.here : "A dependant task cannot have an offset"
      end
      select
        $task$
        $identifier$ ?let masterTaskName
        let @luint every
        select
          every = @luint.init { !1 !@location.here}
        or
          $every$
          $literal_integer$ ? every
        end
        [outTaskMap searchKey !masterTaskName ?let masterTaskIndex ?* ?* ?* ?* ?* ?taskProcessorIndex ?taskPeriod ?*]
        taskKind = @C_taskDependsFromTask.init { !masterTaskIndex !every}
      or
        $message$
        $identifier$ ?let masterMessageName
        [outMessagesMap searchKey !masterMessageName ?let masterMessageIndex ?* ?* ?* ?* ?* ?* ?taskPeriod ?*]
        $processor$
        $identifier$ ?let taskProcessorName
        let @luint every
        select
          every = @luint.init { !1 !@location.here}
        or
          $every$
          $literal_integer$ ? every
        end
        [outProcessorMap searchKey !taskProcessorName ?taskProcessorIndex ?*]
        taskKind = @C_taskDependsFromMessage.init { !masterMessageIndex !every}
      end
    end
    [!?outTaskMap insertKey !taskName ![outTaskMap count]
                                      !taskPriority
                                        !taskOffset
                                        !taskDeadline
                                        !taskDurationMin
                                        !taskDurationMax
                                        !taskProcessorIndex
                                        !taskPeriod
                                        !taskKind]
    $;$
  while
    let @luint messageClass
    select
      $standard$
      messageClass = @luint.init { !0 !@location.here}
      $message$
    or
      $extended$
      messageClass = @luint.init { !1 !@location.here}
      $message$
    or
      $message$
      messageClass = @luint.init { !2 !@location.here}
    end
    $identifier$ ?let messageName
    $network$
    $identifier$ ?let networkName
    [outNetworkMap searchKey !networkName ?let networkIndex ?let CANnetwork ?*]
    if CANnetwork & (messageClass.uint == 2) then
      error messageClass : "a CAN message must be either standard either extended"
    elsif (not CANnetwork) & (messageClass.uint != 2) then
      error messageClass : "a VAN message cannot be standard or extended"
    end
    $length$
    $literal_integer$ ?let bytesCount
    if CANnetwork & (bytesCount.uint > 8) then
      error bytesCount : "CAN message length must be <= 8 bytes"
    elsif (not CANnetwork) & (bytesCount.uint > 28) then
      error bytesCount : "VAN message must be <= 28 bytes"
    end
    $priority$
    $literal_integer$ ?let priority
    let @luint offset
    let @bool explicitOffset
    select
      $offset$
      $literal_integer$ ? offset
      explicitOffset = true
    or
      offset = @luint.init { !0 !@location.here}
      explicitOffset = false
    end
    let @luint deadline
    select
      $deadline$
      $literal_integer$ ? deadline
    or
      deadline = @luint.init { !4294967295 !@location.here}
    end
    let @AC_canMessage messageKind
    let @luint period
    select
      $period$
      $literal_integer$ ? period
      messageKind = @C_canIndependantMessage.init
    or
      $on$
      if explicitOffset then
        error @location.here : "A dependant task cannot have an offset"
      end
      select
        $message$
        $identifier$ ?let name
        [outMessagesMap searchKey !name ?let messageIndex ?* ?* ?* ?* ?* ?* ?period ?*]
        messageKind = @C_canMessageFromMessage.init { !messageIndex}
      or
        $task$
        $identifier$ ?let taskName
        [outTaskMap searchKey !taskName ?let taskIndex ?* ?* ?* ?* ?* ?* ?period ?*]
        messageKind = @C_canMessageFromTask.init { !taskIndex}
      end
    end
    [!?outMessagesMap insertKey !messageName ![outMessagesMap count]
         !messageClass !networkIndex !bytesCount !priority !offset !deadline !period !messageKind]
    $;$        
  end
  $end$
  performComputations (
    !?outProcessorMap
    !?outNetworkMap
    !?outMessagesMap
    !?outTaskMap
  )
}

}